afterEvaluate { project ->
    if (project.getPlugins().hasPlugin('com.android.library')) {
        task androidSourcesJar(type: Jar) {
            classifier 'sources'
            from android.sourceSets.main.java.srcDirs
        }

        task androidJavadoc(type: Javadoc) {
            failOnError false
            source = android.sourceSets.main.java.sourceFiles
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        }

        task androidJavadocJar(type: Jar, dependsOn: androidJavadoc) {
            classifier 'javadoc'
            from androidJavadoc.destinationDir
        }

        androidJavadoc {
            options.encoding 'UTF-8'
            options.locale 'en_US'
        }

        artifacts {
            archives androidSourcesJar
            archives androidJavadocJar
        }
    } else {
        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier 'javadoc'
            from javadoc.destinationDir
        }

        javadoc {
            options.encoding 'UTF-8'
            options.locale 'en_US'
        }

        artifacts {
            archives sourcesJar
            archives javadocJar
        }
    }
}
